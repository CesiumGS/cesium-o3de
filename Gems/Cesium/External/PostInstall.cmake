set(PACKAGE_BUILD_DIR ${CMAKE_CURRENT_LIST_DIR}/Packages)
set(PACKAGE_INSTALL_DIR ${PACKAGE_BUILD_DIR}/Install)
set(PACKAGE_TARBALL_PATH ${PACKAGE_INSTALL_DIR}/CesiumNative.tgz)
set(PACKAGE_TARBALL_SHA256SUM_PATH ${PACKAGE_INSTALL_DIR}/SHA256SUM.txt)

# copy license file to Packages/CesiumNative/CesiumNative
file(COPY 
      ${CMAKE_CURRENT_LIST_DIR}/CesiumNative/LICENSE 
      DESTINATION ${PACKAGE_BUILD_DIR}/CesiumNative/CesiumNative/)

# generate SHA256 for files in Packages/CesiumNative
file(GLOB_RECURSE PACKAGE_FILES 
      LIST_DIRECTORIES false
      ${PACKAGE_BUILD_DIR}/CesiumNative/*)
list(REMOVE_ITEM PACKAGE_FILES "${PACKAGE_BUILD_DIR}/CesiumNative/SHA256SUMS")

set(SHA256SUMS_CONTENT "")
foreach(FILE ${PACKAGE_FILES})
      file(SHA256 ${FILE} FILE_CHECKSUM)
      file(RELATIVE_PATH FILE_RELATIVE_PATH ${PACKAGE_BUILD_DIR}/CesiumNative/ ${FILE})
      set(SHA256SUMS_CONTENT "${SHA256SUMS_CONTENT}${FILE_CHECKSUM} *${FILE_RELATIVE_PATH}\n")
endforeach()

file(WRITE ${PACKAGE_BUILD_DIR}/CesiumNative/SHA256SUMS ${SHA256SUMS_CONTENT})

# create tarball file for Packages/CesiumNative directory
file(MAKE_DIRECTORY ${PACKAGE_INSTALL_DIR})
execute_process(COMMAND ${CMAKE_COMMAND} -E tar "cfz" "${PACKAGE_TARBALL_PATH}" "${PACKAGE_BUILD_DIR}/CesiumNative"
                WORKING_DIRECTORY ${CMAKE_CURRENT_LIST_DIR})

# save tarball SHA256 in a file for o3de
file(SHA256 ${PACKAGE_TARBALL_PATH} TARBALL_CHECKSUM)
file(WRITE ${PACKAGE_INSTALL_DIR}/SHA256SUMS ${TARBALL_CHECKSUM})